plugins {
  id 'java-gradle-plugin'
  id 'checkstyle'
}

group = 'org.terracotta.build'

repositories {
  gradlePluginPortal()
  mavenCentral()
}

dependencies {
  api 'com.gradleup.shadow:shadow-gradle-plugin:9.0.2'
  api 'biz.aQute.bnd:biz.aQute.bndlib:6.4.0'
  implementation 'org.apache.httpcomponents.client5:httpclient5-fluent:5.2.1'
  implementation 'org.terracotta:terracotta-utilities-tools:0.0.16'

  testImplementation platform('org.junit:junit-bom:5.14.2')
  testImplementation 'org.junit.jupiter:junit-jupiter'
  testImplementation 'org.junit.jupiter:junit-jupiter-params'
  testImplementation 'org.hamcrest:hamcrest:2.2'
  testImplementation 'org.mockito:mockito-core:5.10.0'
}

checkstyle {
  configFile = project.file 'config/checkstyle/copyright.xml'
}

testing {
  suites.withType(JvmTestSuite) {
    useJUnitJupiter()
    targets.all {
      testTask.configure {
        jvmArgs '-XX:+EnableDynamicAgentLoading'
      }
    }
  }
}

gradlePlugin {
  plugins {
    angela {
      id = 'org.terracotta.build.angela'
      implementationClass = 'org.terracotta.build.plugins.AngelaPlugin'
    }
    blacklist {
      id = 'org.terracotta.build.blacklist'
      implementationClass = 'org.terracotta.build.plugins.BlacklistPlugin'
    }
    copyright {
      id = 'org.terracotta.build.copyright'
      implementationClass = 'org.terracotta.build.plugins.CopyrightPlugin'
    }
    deploy {
      id = 'org.terracotta.build.deploy'
      implementationClass = 'org.terracotta.build.plugins.DeployPlugin'
    }
    dockerEcosystem {
      id = 'org.terracotta.build.docker-ecosystem'
      implementationClass = 'org.terracotta.build.plugins.docker.DockerEcosystemPlugin'
    }
    docker {
      id = 'org.terracotta.build.docker-build'
      implementationClass = 'org.terracotta.build.plugins.docker.DockerBuildPlugin'
    }
    galvan {
      id = 'org.terracotta.build.galvan'
      implementationClass = 'org.terracotta.build.plugins.GalvanPlugin'
    }
    hosts {
      id = 'org.terracotta.build.hosts'
      implementationClass = 'org.terracotta.build.plugins.CustomLocalHostPlugin'
    }
    javadocFilter {
      id = 'org.terracotta.build.javadoc-annotation'
      implementationClass = 'org.terracotta.build.plugins.JavadocAnnotationPlugin'
    }
    javaVersionPlugin {
      id = 'org.terracotta.build.java-version'
      implementationClass = 'org.terracotta.build.plugins.JavaVersionPlugin'
    }
    packaging {
      id = 'org.terracotta.build.package'
      implementationClass = 'org.terracotta.build.plugins.PackagePlugin'
    }
    osgiPackaging {
      id = 'org.terracotta.build.osgi-package'
      implementationClass = 'org.terracotta.build.plugins.OsgiPackagePlugin'
    }
    voltron {
      id = 'org.terracotta.build.voltron'
      implementationClass = 'org.terracotta.build.plugins.VoltronPlugin'
    }
    buildInfo {
      id = 'org.terracotta.build.build-info'
      implementationClass = 'org.terracotta.build.plugins.buildinfo.BuildInfoPlugin'
    }
    tool {
      id = 'org.terracotta.build.tool'
      implementationClass = 'org.terracotta.build.plugins.ToolPlugin'
    }
  }
}
